apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "$rootDir/tools/tools.gradle"

final app_name = "Desafio Android"

android {

    defaultConfig {
        applicationId "br.com.concrete.desafio"

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    signingConfigs {
        debug {
            storeFile file('../distribution/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('../distribution/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            resValue 'string', 'app_name', app_name + " - DEV"

            signingConfig signingConfigs.debug
            applicationIdSuffix '.dev'
            versionNameSuffix '_dev'

            debuggable true
            testCoverageEnabled true
        }

        release {
            resValue 'string', 'app_name', app_name

            signingConfig signingConfigs.release

            debuggable false

            minifyEnabled true
            useProguard true
            shrinkResources true
        }
    }
}

final versions = rootProject.ext

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    api project(':sdk')

    // Picasso
    implementation "com.squareup.picasso:picasso:$versions.picasso"

    // Android Support
    implementation "com.android.support:design:$versions.androidSupport"
    implementation "com.android.support.constraint:constraint-layout:$versions.constraintLayout"
}