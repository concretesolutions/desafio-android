buildscript {
    ext.kotlin_version = '1.2.71'
    ext.android_plugin_version = '3.2.0'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {

    afterEvaluate {

        if (!project.hasProperty("android")) return

        apply from: "$rootDir/tools/jacoco.gradle"

        android {

            compileSdkVersion 28
            buildToolsVersion "28.0.3"

            defaultConfig {
                minSdkVersion 21
                targetSdkVersion 28

                versionCode 1
                versionName "1.0"

                resConfigs "en"
            }

            lintOptions {
                abortOnError false
            }

            aaptOptions {
                cruncherEnabled = false
            }

            testOptions {
                unitTests.returnDefaultValues = true
            }

            packagingOptions {
                exclude 'META-INF/LICENSE'
            }

            sourceSets {
                main.java.srcDirs += 'src/main/kotlin'
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }
    }
}

ext.versions = [

        // Android
        androidSupport  : '28.0.0',
        constraintLayout: '1.1.3',

        // Google
        gson            : '2.8.5',

        // Square
        picasso         : '2.5.2',
        okHttp          : '3.11.0',
        retrofit        : '2.4.0',
        timber          : '4.7.1',

        // Android Arch
        arch            : '1.1.1',

        // Arch Toolkit
        statemachine    : '0.0.10',
        recycler_adapter: '0.0.6'
]

task clean(type: Delete) {
    delete rootProject.buildDir
}