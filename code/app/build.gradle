apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.concrete.desafioandroid"
        minSdkVersion 21
        targetSdkVersion 27

        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 0
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        setProperty("archivesBaseName", "desafio-android-$versionName")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
    }

    flavorDimensions "default"
    productFlavors {
        development {
            applicationIdSuffix ".development"
            buildConfigField "String", 'API_ENDPOINT', '"https://api.github.com/"'

            dimension "default"
        }
        production {
            buildConfigField "String", 'API_ENDPOINT', '""'

            dimension "default"
        }
    }

    //Signing
    File signFile = file('sign.properties')
    if (signFile.exists()) {
        Properties p = new Properties()
        p.load(new FileInputStream(signFile))
        signingConfigs {
            release {
                storeFile file(p.releaseKeyStorePath)
                storePassword p.releaseKeyStorePassword
                keyAlias p.releaseKeyAlias
                keyPassword p.releaseKeyPassword
            }
        }

        buildTypes.release.signingConfig signingConfigs.release

    } else {
        throw new GradleException("sign.properties file not found")
    }

}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.1'

    implementation 'org.kodein.di:kodein-di-generic-jvm:5.2.0'
    implementation 'org.kodein.di:kodein-di-framework-android-support:5.2.0'
}
