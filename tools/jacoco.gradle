apply plugin: 'jacoco'

def jacocoRetrolambdaMap = [:]

jacoco {
    toolVersion = '0.7.4+'
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    jacocoClasspath = configurations['androidJacocoAnt']

    final fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*',
                        '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*',
                        '**/*_.*', '**/*_*.*', '**/*_$*.*', '**/*$ViewBinder*.*',
                        '**/*$ViewBinding*.*', '**/*$Lambda$*', '**/*$lambda$*',
                        '**/lambda$*']

    final fileFilterKt = fileFilter + ['**/*$*$*.*']

    final debugTreeJava = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    final debugTreeKotlin = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilterKt)
    final mainSrcJava = "${project.projectDir}/src/main/java"
    final mainSrcKotlin = "${project.projectDir}/src/main/kotlin"

    sourceDirectories = files([mainSrcJava, mainSrcKotlin])
    classDirectories = files([debugTreeJava, debugTreeKotlin])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ])

    doFirst {
        new File("${buildDir}/intermediates/classes/debug").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                final renamed = file.path.replace '$$', '$'
                jacocoRetrolambdaMap[renamed] = file.path
                file.renameTo renamed
            }
        }
    }

    doLast {
        // Revert renamed files back to their generated names
        jacocoRetrolambdaMap.each { renamed, original -> new File(renamed).renameTo original }
        println "JaCoCo report has been generated to file://${reports.html.destination}/index.html"
    }
}